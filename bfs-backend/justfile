set dotenv-filename := ".env.local"

BIN := justfile_directory() / "bin"
FLYWAY_IMAGE := "flyway/flyway:9-alpine"

# ── Private ──

[private]
_go-tool bin pkg:
    #!/usr/bin/env bash
    [[ -x "{{BIN}}/{{bin}}" ]] && exit 0
    mkdir -p "{{BIN}}" && GOBIN="{{BIN}}" go install "{{pkg}}"

# ── Setup ──

# Install all dev tools
[group('setup')]
tools: (_go-tool "golangci-lint" "github.com/golangci/golangci-lint/cmd/golangci-lint@v2.5.0") (_go-tool "swag" "github.com/swaggo/swag/cmd/swag@v1.16.6") (_go-tool "air" "github.com/air-verse/air@v1.63.0")

# ── Dev ──

# Start API server with live-reload
[group('dev')]
dev: (_go-tool "air" "github.com/air-verse/air@v1.63.0")
    "{{BIN}}/air" -c .air.toml

# Start services (pass --profile dev for pgAdmin)
[group('dev')]
up *args:
    docker compose up -d {{args}}

# Stop services (pass --volumes for DATA LOSS)
[group('dev')]
down *args:
    docker compose --profile dev --profile observability down --remove-orphans {{args}}

# Follow container logs
[group('dev')]
logs:
    docker compose logs -f

# Show running containers
[group('dev')]
ps:
    docker compose ps -a

# Clean build artifacts
[group('dev')]
clean:
    rm -rf tmp/ build-errors.log docs/

# ── Lint ──

# Run linters (pass --fix to auto-fix)
[group('lint')]
lint *args: (_go-tool "golangci-lint" "github.com/golangci/golangci-lint/cmd/golangci-lint@v2.5.0")
    GOTOOLCHAIN=auto GOWORK=off "{{BIN}}/golangci-lint" run {{args}} --timeout=3m --max-issues-per-linter=0 --max-same-issues=0 ./...

# Format code
[group('lint')]
fmt:
    go fmt ./...

# Tidy go.mod
[group('lint')]
tidy:
    GOWORK=off go mod tidy

# Verify go.mod is tidy (CI)
[group('lint')]
tidy-check: tidy
    git diff --exit-code -- go.mod go.sum

# ── Generate ──

# Run all code generators
[group('generate')]
generate: generate-ent generate-api

# Generate Ent ORM code
[group('generate')]
generate-ent:
    mkdir -p ./internal/generated/ent
    echo "package ent" > ./internal/generated/ent/ent.go
    go run -mod=mod entgo.io/ent/cmd/ent generate --feature sql/modifier --feature sql/upsert --target ./internal/generated/ent ./internal/schema

# Generate API types from OpenAPI
[group('generate')]
generate-api:
    npx @redocly/cli bundle openapi/openapi.yaml -o openapi/bundled.yaml
    oapi-codegen -config oapi-codegen.yaml openapi/bundled.yaml

# Generate Swagger docs
[group('generate')]
swag: (_go-tool "swag" "github.com/swaggo/swag/cmd/swag@v1.16.6")
    "{{BIN}}/swag" init -g cmd/backend/main.go --parseInternal --parseDependency --generatedTime=false

# ── Test ──

# Run unit tests (override path: just test ./...)
[group('test')]
test *args='./internal/...':
    go test -v -race {{args}}

# Run integration tests
[group('test')]
test-integration:
    go test -v -race -count=1 -timeout=5m ./test/integration/...

# ── Database ──

# Run flyway migration command
[group('db')]
migrate cmd='migrate':
    docker run --rm --network=host \
        -v "{{justfile_directory()}}/db/flyway/migrations:/flyway/sql:ro" \
        -e FLYWAY_URL="jdbc:postgresql://localhost:5432/bless2n_food_system" \
        -e FLYWAY_USER="postgres" -e FLYWAY_PASSWORD="postgres" \
        {{FLYWAY_IMAGE}} -defaultSchema=app -createSchemas=true -baselineOnMigrate=true {{cmd}}

# Drop all objects (DATA LOSS)
[confirm("⚠️  This will DROP ALL OBJECTS. Continue?")]
[group('db')]
migrate-clean:
    just migrate "-cleanDisabled=false clean"

# Seed dev data
[group('db')]
seed:
    #!/usr/bin/env bash
    for f in db/seed/*.sql; do echo "  $f..."; psql "$DATABASE_URL" -f "$f"; done
